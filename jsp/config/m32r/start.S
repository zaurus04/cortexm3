/*
 *  TOPPERS/JSP Kernel
 *      Toyohashi Open Platform for Embedded Real-Time Systems/
 *      Just Standard Profile Kernel
 * 
 *  Copyright (C) 2000-2003 by Embedded and Real-Time Systems Laboratory
 *                              Toyohashi Univ. of Technology, JAPAN
 *  Copyright (C) 2007 by Embedded and Real-Time Systems Laboratory
 *              Graduate School of Information Science, Nagoya Univ., JAPAN
 * 
 *  上記著作権者は，以下の (1)〜(4) の条件か，Free Software Foundation 
 *  によって公表されている GNU General Public License の Version 2 に記
 *  述されている条件を満たす場合に限り，本ソフトウェア（本ソフトウェア
 *  を改変したものを含む．以下同じ）を使用・複製・改変・再配布（以下，
 *  利用と呼ぶ）することを無償で許諾する．
 *  (1) 本ソフトウェアをソースコードの形で利用する場合には，上記の著作
 *      権表示，この利用条件および下記の無保証規定が，そのままの形でソー
 *      スコード中に含まれていること．
 *  (2) 本ソフトウェアを，ライブラリ形式など，他のソフトウェア開発に使
 *      用できる形で再配布する場合には，再配布に伴うドキュメント（利用
 *      者マニュアルなど）に，上記の著作権表示，この利用条件および下記
 *      の無保証規定を掲載すること．
 *  (3) 本ソフトウェアを，機器に組み込むなど，他のソフトウェア開発に使
 *      用できない形で再配布する場合には，次のいずれかの条件を満たすこ
 *      と．
 *    (a) 再配布に伴うドキュメント（利用者マニュアルなど）に，上記の著
 *        作権表示，この利用条件および下記の無保証規定を掲載すること．
 *    (b) 再配布の形態を，別に定める方法によって，TOPPERSプロジェクトに
 *        報告すること．
 *  (4) 本ソフトウェアの利用により直接的または間接的に生じるいかなる損
 *      害からも，上記著作権者およびTOPPERSプロジェクトを免責すること．
 * 
 *  本ソフトウェアは，無保証で提供されているものである．上記著作権者お
 *  よびTOPPERSプロジェクトは，本ソフトウェアに関して，その適用可能性も
 *  含めて，いかなる保証も行わない．また，本ソフトウェアの利用により直
 *  接的または間接的に生じたいかなる損害に関しても，その責任を負わない．
 * 

 *  @(#) $Id: start.S,v 1.1 2008/06/17 00:04:58 suikan Exp $
 */

#define _MACRO_ONLY

#include <m32rasm.inc>
#include <s_services.h>
		
/*
 *  スタートアップルーチン 
 */
	.text
	.align 4
	.globl start

start:
	.extern		kernel_start
	.extern		hardware_init_hook
	.extern		software_init_hook
	.extern 	reset

	/* pswの初期化 */
	ldi		r0, 0	
	mvtc	r0, psw

	/* 割込みスタックポインタの初期化 */
	lea		r15, __stack
	
	/* EITベースレジスタの設定 */
	lea		r0, reset
	mvtc	r0, cr5

	/* ハードウェア初期化ルーチンの起動 */
	lea		r0, hardware_init_hook
	beqz	r0, L1
	jl		r0

L1:
	/* .bssセクションのクリア */
	xor		r0, r0
	lea		r1, __sbss
	lea		r2, __ebss
	beq		r1, r2, L3
L2:
	st		r0, @-r2
	bne		r1, r2, L2

L3:
	/* .dataセクションの初期化 */
	lea		r1, __sidata
	lea		r2, __eidata
	lea		r3, __sdata

	addi	r3, -4
	beq		r1, r2, L5
L4:	ld		r0, @r1+
	st		r0, @+r3
	bne		r1, r2, L4

L5:
	/* ソフトウェア初期化ルーチンの起動 */
	lea		r0, software_init_hook
	beqz	r0, L6
	jl		r0

L6:
	/* 割込みマスクの設定 */
	ld24 	r0, ICUIMASK + 1
	ldi 	r1, 7				/* すべての割込み要求を受付け */
	stb 	r1, @r0 			

	/* カーネル起動 */
	bl		kernel_start

L7:	bra		L7
